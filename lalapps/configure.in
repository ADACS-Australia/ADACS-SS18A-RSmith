dnl Process this file with autoconf to produce a configure script.
dnl $Id$
AC_INIT(src/config.h.in)
AM_INIT_AUTOMAKE(lalapps, 3.0)
AM_CONFIG_HEADER(src/config.h)

dnl Put version/configure info in config.h header
lalapps_version_major=`echo "$VERSION" | cut -d. -f1`
lalapps_version_minor=`echo "$VERSION" | cut -d. -f2`
lalapps_configure_date=`date +"%Y-%m-%dT%H:%M:%S%z"`
lalapps_cvs_tag=`echo '$Name$' | sed 's/\\$//g' | sed 's/Name: //'`
AC_DEFINE_UNQUOTED(LALAPPS_VERSION, "$VERSION", [LALApps Version])
AC_DEFINE_UNQUOTED(LALAPPS_VERSION_MAJOR, $lalapps_version_major,
                   [LALApps Version Major Number])
AC_DEFINE_UNQUOTED(LALAPPS_VERSION_MINOR, $lalapps_version_minor,
                   [LALApps Version Minor Number])
AC_DEFINE_UNQUOTED(LALAPPS_CONFIGURE_ARGS, "$ac_configure_args",
                   [LALApps Configure Arguments])
AC_DEFINE_UNQUOTED(LALAPPS_CONFIGURE_DATE, "$lalapps_configure_date",
                   [LALApps Configure Date])
AC_DEFINE_UNQUOTED(LALAPPS_CVS_TAG, "$lalapps_cvs_tag", [LALApps CVS Tag])

LALAPPS_WITH_CC
LALAPPS_WITH_EXTRA_CPPFLAGS
LALAPPS_WITH_EXTRA_CFLAGS
LALAPPS_WITH_EXTRA_LDFLAGS
LALAPPS_WITH_EXTRA_LIBS
LALAPPS_ENABLE_CONDOR
LALAPPS_ENABLE_FRAME
LALAPPS_ENABLE_DATAFLOW

dnl Checks for programs.
AC_PROG_AWK
AC_PROG_CC
AC_PROG_CPP
AC_CANONICAL_HOST
if test "x$condor" = "xtrue"; then
  AC_CHECK_PROGS(CONDOR_COMPILE, condor_compile)
  CC="$CONDOR_COMPILE $CC"
  case "$host" in
    *-*-linux* | *-*-osf* | *-*-hpux* ) CFLAGS="$CFLAGS -static" AC_DISABLE_SHARED;;
  esac
  AC_DEFINE(LALAPPS_CONDOR, 1, [LALApps is condor compiled])
fi
if test "$GCC" = yes; then
  CPPFLAGS="$CPPFLAGS -ansi"
  CFLAGS="$CFLAGS -W -Wall"
fi
AC_PROG_INSTALL
AC_PROG_LN_S
AM_PROG_LIBTOOL
AC_CHECK_PROGS(LATEX, pdflatex latex, echo)
AC_CHECK_PROGS(MKIND, makeindex, echo)
AC_CHECK_PROGS(DVIPS, dvips, echo)
AC_CHECK_PROGS(BIBTEX, bibtex, echo)
AC_CHECK_PROGS(PYTHON, python2.2, echo)

dnl Checks for libraries.
AC_CHECK_LIB(m, sin)
if test "${frame}" = "true"; then
  AC_CHECK_LIB(Frame, FrLibIni, , LALAPPS_DISABLE_FRAME)
fi
AC_CHECK_LIB(lal, LALVersion, , AC_MSG_ERROR(could not find the LAL library))
if test "${dataflow}" = "true"; then
  AC_SEARCH_LIBS(MetaioOpen, metaio dataflow, , AC_MSG_ERROR(could not find the metaio/dataflow library))
fi
AC_CHECK_LIB(lalsupport, LALOpenDataFile, , AC_MSG_ERROR(could not find the LAL support library))
lalframe=true
lalmetaio=true
AC_CHECK_LIB(lalframe, LALFrOpen, , lalframe=false)
AC_CHECK_LIB(lalmetaio, LALSnglInspiralTableFromLIGOLw, , lalmetaio=false)
AC_CHECK_LIB(gsl, gsl_version)
AC_CHECK_LIB(mysqlclient, mysql_init)

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(getopt.h)
AC_CHECK_HEADERS([gsl/gsl_fft_real.h])
AC_CHECK_HEADERS(mysql.h)
AC_CHECK_HEADERS(mpi.h)
AC_CHECK_HEADERS([lal/LALStdlib.h])
if test "${frame}" = "true"; then
  AC_CHECK_HEADERS(FrameL.h, , LALAPPS_DISABLE_FRAME)
fi
if test "${dataflow}" = "true"; then
  AC_CHECK_HEADERS(metaio.h, , AC_MSG_ERROR(could not find metaio.h))
fi

LALAPPS_CHECK_QTHREAD

AC_MSG_CHECKING(for gethostname prototype in unistd.h)
AC_EGREP_HEADER(gethostname,unistd.h,AC_MSG_RESULT(yes)
AC_DEFINE(HAVE_GETHOSTNAME_PROTOTYPE,1,
[Define if gethostname prototype is in unistd.h]),
AC_MSG_RESULT(no))

AM_CONDITIONAL(FRAME, test x$frame = xtrue)
AM_CONDITIONAL(LALFRAME, test x$lalframe = xtrue)
AM_CONDITIONAL(LALMETAIO, test x$lalmetaio = xtrue)
AM_CONDITIONAL(DATAFLOW, test x$dataflow = xtrue)
AM_CONDITIONAL(PYTHON22, test x$PYTHON = "xpython2.2" )
AC_DEFINE_UNQUOTED(PREFIX, "$prefix", [Install prefix])

AC_OUTPUT( 				\
	Makefile 			\
	doc/Makefile 			\
	man/Makefile 			\
	src/Makefile			\
	src/lalapps/Makefile            \
	src/example/Makefile		\
	src/calibration/Makefile	\
        src/frametools/Makefile         \
	src/findchirp/Makefile		\
	src/stochastic/Makefile		\
	src/power/Makefile		\
	src/ring/Makefile		\
	src/pulsar/Makefile		\
	src/pulsar/FDS_isolated/Makefile \
	src/pulsar/Injections/Makefile	 \
	src/inspiral/Makefile		\
	src/zmsearch/Makefile		\
	src/detresponse/Makefile	\
	src/geoinspiralsearch/Makefile  \
)
