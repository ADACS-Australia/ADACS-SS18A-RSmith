dnl Process this file with autoconf to produce a configure script.
AC_INIT(src/config.h.in)
AM_INIT_AUTOMAKE(lalapps, 1.1)
AM_CONFIG_HEADER(src/config.h)

LALAPPS_WITH_CC
LALAPPS_WITH_EXTRA_CPPFLAGS
LALAPPS_WITH_EXTRA_CFLAGS
LALAPPS_WITH_EXTRA_LDFLAGS
LALAPPS_WITH_EXTRA_LIBS
LALAPPS_ENABLE_CONDOR
LALAPPS_ENABLE_FRAME
LALAPPS_ENABLE_DATAFLOW

dnl Checks for programs.
AC_PROG_AWK
AC_PROG_CC
AC_PROG_CPP
AC_CANONICAL_HOST
if test "x$condor" = "xtrue"; then
  AC_CHECK_PROGS(CONDOR_COMPILE, condor_compile)
  CC="$CONDOR_COMPILE $CC"
  case "$host" in
    *-*-linux* | *-*-osf* | *-*-hpux* ) CFLAGS="$CFLAGS -static" AC_DISABLE_SHARED;;
  esac
fi
if test "$GCC" = yes; then
  CFLAGS="$CFLAGS -ansi -W -Wall"
fi
AC_PROG_INSTALL
AC_PROG_LN_S
AM_PROG_LIBTOOL
AC_CHECK_PROGS(LATEX, pdflatex latex, echo)
AC_CHECK_PROGS(MKIND, makeindex, echo)
AC_CHECK_PROGS(DVIPS, dvips, echo)
AC_CHECK_PROGS(BIBTEX, bibtex, echo)

dnl Checks for libraries.
AC_CHECK_LIB(m, sin)
if test "${frame}" = "true"; then
  AC_CHECK_LIB(Frame, FrLibIni, , LALAPPS_DISABLE_FRAME)
fi
rm -f confjunk.c
if test "${dataflow}" = "true"; then
  AC_CHECK_LIB(dataflow, MetaioOpen, , AC_MSG_ERROR(could not find the dataflow library))
fi
rm -f confjunk.c
echo 'int lalDebugLevel=0;' > confjunk.c
$CC -c confjunk.c
dnl LALAPPS_CHECK_LAL
AC_CHECK_LIB(lal, LALVersion, ,
  AC_MSG_ERROR(could not find the LAL library), confjunk.o)
AC_CHECK_LIB(lalsupport, LALOpenDataFile, ,
  AC_MSG_ERROR(could not find the LAL support library), confjunk.o)
AC_CHECK_LIB(lalframe, LALFrOpen, , , confjunk.o)
rm -f confjunk.c confjunk.o

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS([lal/LALStdlib.h])
if test "${frame}" = "true"; then
  AC_CHECK_HEADERS(FrameL.h, , LALAPPS_DISABLE_FRAME)
fi
if test "${dataflow}" = "true"; then
  AC_CHECK_HEADERS(metaio.h, , AC_MSG_ERROR(could not find metaio.h))
fi

AM_CONDITIONAL(FRAME, test x$frame = xtrue)

AC_OUTPUT( 				\
	Makefile 			\
	doc/Makefile 			\
	man/Makefile 			\
	src/Makefile			\
	src/lalapps/Makefile            \
	src/example/Makefile		\
	src/calibration/Makefile	\
        src/frametools/Makefile         \
	src/findchirp/Makefile		\
	src/stochastic/Makefile		\
	src/power/Makefile		\
	src/ring/Makefile		\
)
